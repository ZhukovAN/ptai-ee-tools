plugins {
    // https://github.com/jenkinsci/gradle-jpi-plugin
    id 'org.jenkins-ci.jpi'
}

group = "${rootGroup}"
description = 'This plugin used to implement AST using PT Application Inspector'
version = "${version}"

repositories {
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

ext {
    // Just placeholders for dependencies / test Jenkins
    // plugin versions. Actual versions are calculated
    // in processJenkinsVersion function
    tokenMacroVersion = ''
    credentialsVersion = ''
    structsVersion = ''
    workflowStepApiVersion = ''
    scmApiVersion = ''
    trileadApiVersion = ''
    scriptSecurityVersion = ''
    matrixProjectVersion = ''
    gitVersion = ''
}

jenkinsPlugin {
    // version of Jenkins core this plugin depends on, must be 1.420 or later
    jenkinsVersion = findProperty('jenkinsVersion')

    // ID of the plugin, defaults to the project name without trailing '-plugin'
    shortName = 'ptai-jenkins-plugin'

    // human-readable name of plugin
    displayName = 'PT Application Inspector Plugin'

    // URL for plugin on Jenkins wiki or elsewhere
    url = 'https://www.ptsecurity.com/ww-en/products/ai/'

    // plugin URL on GitHub, optional
    gitHubUrl = 'https://github.com/PositiveTechnologies/ptaiPlugins'

    // use the plugin class loader before the core class loader, defaults to false
    // pluginFirstClassLoader = true

    // optional list of package prefixes that your plugin doesn't want to see from core
    // maskClasses = 'groovy.grape org.apache.commons.codec'

    // optional version number from which this plugin release is configuration-compatible
    // compatibleSinceVersion = '1.1.0'

    // set the directory from which the development server will run, defaults to 'work'
    // workDir = file('/tmp/jenkins')

    // URL used to deploy the plugin, defaults to the value shown
    // the system property 'jpi.repoUrl' can be used to override this option
    // repoUrl = 'https://repo.jenkins-ci.org/releases'

    // URL used to deploy snapshots of the plugin, defaults to the value shown
    // the system property 'jpi.snapshotRepoUrl' can be used to override this option
    // snapshotRepoUrl = 'https://repo.jenkins-ci.org/snapshots'

    // enable injection of additional tests for checking the syntax of Jelly and other things
    // disabledTestInjection = false

    // the output directory for the localizer task relative to the project root, defaults to the value shown
    // localizerOutputDir = "${project.buildDir}/generated-src/localizer"

    // disable configuration of Maven Central, the local Maven cache and the Jenkins Maven repository, defaults to true
    configureRepositories = false

    // skip configuration of publications and repositories for the Maven Publishing plugin, defaults to true
    configurePublishing = false

    // plugin file extension, either 'jpi' or 'hpi', defaults to 'hpi'
    fileExtension = 'hpi'

    // the developers section is optional, and corresponds to the POM developers section
    developers {
        developer {
            id 'ZhukovAN'
            name 'Alexey Zhukov'
            email 'alexey.n.zhukov@gmail.com'
        }
    }

    // the licenses section is optional, and corresponds to the POM licenses section
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
            comments 'A business-friendly OSS license'
        }
    }
}

configurations {
    webjars
}

dependencies {
    api project(':generic-client-lib')
    api project(':ptai-report-tools')
    testImplementation(testFixtures(project(':generic-client-lib')))

    def version = processJenkinsVersion()
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/token-macro
    implementation "org.jenkins-ci.plugins:token-macro:${tokenMacroVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/credentials
    implementation "org.jenkins-ci.plugins:credentials:${credentialsVersion}"

    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/token-macro
    jenkinsServer "org.jenkins-ci.plugins:token-macro:${tokenMacroVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/credentials
    jenkinsServer "org.jenkins-ci.plugins:credentials:${credentialsVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/structs
    jenkinsServer "org.jenkins-ci.plugins:structs:${structsVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins.workflow/workflow-step-api
    jenkinsServer "org.jenkins-ci.plugins.workflow:workflow-step-api:${workflowStepApiVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/scm-api
    jenkinsServer "org.jenkins-ci.plugins:scm-api:${scmApiVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/trilead-api
    jenkinsServer "org.jenkins-ci.plugins:trilead-api:${trileadApiVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/script-security
    jenkinsServer "org.jenkins-ci.plugins:script-security:${scriptSecurityVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/matrix-project
    jenkinsServer "org.jenkins-ci.plugins:matrix-project:${matrixProjectVersion}"
    // https://mvnrepository.com/artifact/org.jenkins-ci.plugins/git
    jenkinsServer "org.jenkins-ci.plugins:git:${gitVersion}"

    // Special includes for pipeline testing
    if ("2.303.1".equals(version)) {
        api platform("io.jenkins.tools.bom:bom-2.302.x:923.v08bdc07cd40f")
        jenkinsServer "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6"
    } else if ("2.277.4".equals(version)) {
        api platform("io.jenkins.tools.bom:bom-2.277.x:937.v51fde92016ed")
        jenkinsServer "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5"
    } else if ("2.164.3".equals(version)) {
        api platform("io.jenkins.tools.bom:bom-2.164.x:10")
        jenkinsServer "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5"
    } else if ("2.150.3".equals(version)) {
        api platform("io.jenkins.tools.bom:bom-2.150.x:8")
        jenkinsServer "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5"
    }
    // Uncomment this to look how DC trend charts are look like
    // jenkinsServer "org.jenkins-ci.plugins:dependency-check-jenkins-plugin:5.1.1"

    // https://mvnrepository.com/artifact/org.webjars/echarts
    webjars 'org.webjars:echarts:5.0.2'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    // https://mvnrepository.com/artifact/commons-validator/commons-validator
    implementation 'commons-validator:commons-validator:1.6'
    // https://mvnrepository.com/artifact/org.jooq/joor-java-8
    implementation 'org.jooq:joor-java-8:0.9.13'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-compress
    implementation 'org.apache.commons:commons-compress:1.21'
    // Need this to work with 7zip compression
    // https://mvnrepository.com/artifact/org.tukaani/xz
    implementation 'org.tukaani:xz:1.9'

    // Need this to execute Jenkins plugin integration tests
    // https://mvnrepository.com/artifact/org.jenkins-ci.main/jenkins-test-harness
    testImplementation group: 'org.jenkins-ci.main', name: 'jenkins-test-harness', version: '1690.vfcb_959006c07'
    // https://mvnrepository.com/artifact/org.jenkins-ci.main/jenkins-war
    testImplementation "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    testImplementation(testFixtures(project(":ptai-data-structures")))
    testImplementation(testFixtures(project(":ptai-rest-api")))
    testImplementation(testFixtures(project(":generic-client-lib")))
}

// Retuns -1 if version a is before b, 0 if those are equal and 1 if a is after b
def compareVersion(a, b) {
    List versionA = a.tokenize('.')
    List versionB = b.tokenize('.')

    def commonIndices = Math.min(versionA.size(), versionB.size())
    for (int i = 0; i < commonIndices; ++i) {
        def numberA = versionA[i].toInteger()
        def numberB = versionB[i].toInteger()
        if (numberA != numberB)
            return numberA <=> numberB
    }
    // If we got this far then all the common indices are identical, so whichever version is longer must be more recent
    return versionA.size() <=> versionB.size()
}

def processJenkinsVersion() {
    String versionString = findProperty('jenkinsVersion')
    if (!versionString) {
        logger.quiet("Jenkins version is not defined, using 2.164.3 as default")
        versionString = '2.164.3'
    }
    logger.info("Using Jenkins version: $versionString")

    def versionMatcher = versionString =~ /(\d+)\.(\d+)\.(\d+)/
    if (!versionMatcher.matches()) {
        versionMatcher = versionString =~ /(\d+)\.(\d+)/
        if (!versionMatcher.matches()) throw new GradleException("Jenkins version syntax incorrect: failed to parse $versionString")
    }
    def version = [versionMatcher.group(1).toInteger(), versionMatcher.group(2).toInteger()]
    if (-1 != compareVersion('2.150', versionString))
        throw new GradleException("Jenkins versions 2.150 and upper are supported only")

    // Calculate structs-plugin version. For Jenkins 2.107.3 version 1.21 is to be used, for 2.222.4 - 1.22
    if (-1 == compareVersion('2.222.4', versionString))
    // https://updates.jenkins.io/download/plugins/structs/1.22/structs.hpi
        structsVersion = '1.22'
    else
    // https://updates.jenkins.io/download/plugins/structs/1.21/structs.hpi
        structsVersion = '1.21'
    logger.info("Using Structs plugin version: $structsVersion")
    // workflow-step-api: 2.22 -> 2.138.4, 2.23 -> 2.176.4
    if (-1 == compareVersion('2.176.4', versionString))
    // https://updates.jenkins.io/download/plugins/workflow-step-api/2.23/workflow-step-api.hpi
        workflowStepApiVersion = '2.23'
    else
    // https://updates.jenkins.io/download/plugins/workflow-step-api/2.22/workflow-step-api.hpi
        workflowStepApiVersion = '2.22'
    logger.info("Using Workflow step API plugin version: $workflowStepApiVersion")
    // token-macro:
    // 2.12 -> 2.121.3
    // 2.13 -> 2.222.3
    // 2.14 -> 2.274
    if (-1 == compareVersion('2.274', versionString))
    // https://updates.jenkins.io/download/plugins/token-macro/2.14/token-macro.hpi
        tokenMacroVersion = '2.14'
    else if (-1 == compareVersion('2.222.3', versionString))
    // https://updates.jenkins.io/download/plugins/token-macro/2.13/token-macro.hpi
        tokenMacroVersion = '2.13'
    else
    // https://updates.jenkins.io/download/plugins/token-macro/2.12/token-macro.hpi
        tokenMacroVersion = '2.12'
    logger.info("Using Token macro plugin version: $tokenMacroVersion")
    // credentials:
    // 2.3.11 -> 2.138.4
    // 2.3.12 -> 2.222.4
    if (-1 == compareVersion('2.222.4', versionString))
    // https://updates.jenkins.io/download/plugins/credentials/2.3.12/credentials.hpi
        credentialsVersion = '2.3.12'
    else
    // https://updates.jenkins.io/download/plugins/credentials/2.3.11/credentials.hpi
        credentialsVersion = '2.3.11'
    logger.info("Using Credentials plugin version: $credentialsVersion")
    // scm-api:
    // 2.4.1 -> 2.60.3
    // 2.5.0 - 2.6.2 -> 2.164.3
    // 2.6.3 -> 2.107.3
    // 2.6.4 -> 2.176.4
    if (-1 == compareVersion('2.176.4', versionString))
    // https://updates.jenkins.io/download/plugins/scm-api/2.6.4/scm-api.hpi
        scmApiVersion = '2.6.4'
    else if (-1 == compareVersion('2.164.3', versionString))
    // https://updates.jenkins.io/download/plugins/scm-api/2.6.2/scm-api.hpi
        scmApiVersion = '2.6.2'
    else
    // https://updates.jenkins.io/download/plugins/scm-api/2.6.3/scm-api.hpi
        scmApiVersion = '2.6.3'
    logger.info("Using SCM API plugin version: $scmApiVersion")
    // trilead-api:
    // 1.0.3 -> 2.150.1
    // 1.0.4 - 1.0.8 -> 2.184
    // 1.0.9 -> 2.204
    // 1.0.10 -> 2.184
    // 1.0.12 - 1.0.13 -> 2.204
    if (-1 == compareVersion('2.204', versionString))
    // https://updates.jenkins.io/download/plugins/trilead-api/1.0.13/trilead-api.hpi
        trileadApiVersion = '1.0.13'
    else if (-1 == compareVersion('2.184', versionString))
    // https://updates.jenkins.io/download/plugins/trilead-api/1.0.10/trilead-api.hpi
        trileadApiVersion = '1.0.10'
    else
    // https://updates.jenkins.io/download/plugins/trilead-api/1.0.3/trilead-api.hpi
        trileadApiVersion = '1.0.3'
    logger.info("Using Trilead API plugin version: $trileadApiVersion")
    // script-security:
    // 1.71 -> 2.60.3
    // 1.72 - 1.76 -> 2.176.4
    // 1.77 -> 2.222.4
    if (-1 == compareVersion('2.222.4', versionString))
    // https://updates.jenkins.io/download/plugins/script-security/1.77/script-security.hpi
        scriptSecurityVersion = '1.77'
    else if (-1 == compareVersion('2.176.4', versionString))
    // https://updates.jenkins.io/download/plugins/script-security/1.76/script-security.hpi
        scriptSecurityVersion = '1.76'
    else
    // https://updates.jenkins.io/download/plugins/script-security/1.71/script-security.hpi
        scriptSecurityVersion = '1.71'
    logger.info("Using Script security plugin version: $scriptSecurityVersion")
    // matrix project:
    // 1.14 -> 2.7.3
    // 1.15 - 1.18 -> 2.164.3
    // 1.19 -> 2.282
    if (-1 == compareVersion('2.282', versionString))
    // https://updates.jenkins.io/download/plugins/matrix-project/1.19/matrix-project.hpi
        matrixProjectVersion = '1.19'
    else if (-1 == compareVersion('2.164.3', versionString))
    // https://updates.jenkins.io/download/plugins/matrix-project/1.18/matrix-project.hpi
        matrixProjectVersion = '1.18'
    else
    // https://updates.jenkins.io/download/plugins/matrix-project/1.14/matrix-project.hpi
        matrixProjectVersion = '1.14'
    // git:
    // 4.2.2 -> 2.138.4
    // 4.3.0 - 4.4.5 -> 2.204.1
    // 4.5.0 - 4.6.0 -> 2.222.4
    // 4.7.0 - 4.7.2 -> 2.263.1
    if (-1 == compareVersion('2.263.1', versionString))
    // https://updates.jenkins.io/download/plugins/git/4.7.2/git.hpi
        gitVersion = '4.7.2'
    else if (-1 == compareVersion('2.222.4', versionString))
    // https://updates.jenkins.io/download/plugins/git/4.6.0/git.hpi
        gitVersion = '4.6.0'
    else if (-1 == compareVersion('2.204.1', versionString))
    // https://updates.jenkins.io/download/plugins/git/4.4.5/git.hpi
        gitVersion = '4.4.5'
    else
    // https://updates.jenkins.io/download/plugins/git/4.2.2/git.hpi
        gitVersion = '4.2.2'
    logger.info("Using Git plugin version: $gitVersion")

    // Starting with 2.264 (https://www.jenkins.io/blog/2020/11/10/major-changes-in-weekly-releases/)
    // need to use different UI
    boolean newUi = (1 != compareVersion('2.264', versionString))
    if (newUi)
        sourceSets.main.resources.srcDirs += [ 'src/main/resources-taglib-2-264+' ]
    else
        sourceSets.main.resources.srcDirs += [ 'src/main/resources-taglib-2-264-' ]
    return versionString
}

// Set Jenkins server debug port
tasks.named('server').configure {
    execSpec {
        debugOptions {
            port.set(8000)
            suspend.set(false)
        }
    }
}

def unpackWebJars = tasks.register('unpack-webjars') {
    doLast {
        // Iterate through JARs from "webjars" configuration
        configurations.webjars.each {jar ->
            def config = configurations.webjars.resolvedConfiguration
            def artifact = config.resolvedArtifacts.find {
                it.file.toString() == jar.absolutePath
            }

            def upStreamVersion = "${artifact.moduleVersion.id.version}"
            upStreamVersion = upStreamVersion.replaceAll(/(-[\d.-]+)/, '')

            def innerJar = "${buildDir}/webjars-content/${artifact.name}"
            logger.quiet("Extracting webjar contents from ${innerJar}")

            copy {
                from zipTree(jar)
                into file("${innerJar}")
            }

            // As webjar contents is a 3rd party code, it's better to avoid putting it
            // to sources / resources folders as files in those folders are pushed to repository.
            // So the best way would be to copy webjar contents to something
            // like $buildDir/generated-resources/webapp and setup JPI task as:
            // tasks.getByName('jpi') {
            //    dependsOn unpackWebJars
            //    from "$buildDir/generated-resources/webapp"
            // }
            // This approach allows us to create HPI file that contains both custom code
            // from src/main/webapp directory and webjars from generated-resources. But
            // Gradle JPI plugin 'server' task generates HPL file that points strictly
            // to webapp folder so Jenkins server started in debug mode using 'gradlew server'
            // command can see only the files from webapp and lacks ones from other folders
            // like generated-sources. So the only solution here is to unpack webjar
            // contents (i.e. JS, CSS, etc.) to webapp/webjars folder and add that folder to .github.
            copy {
                from "${innerJar}/META-INF/resources/webjars/${artifact.name}/${upStreamVersion}",
                        "${innerJar}/META-INF/resources/webjars/${artifact.name}/${artifact.moduleVersion.id.version}"

                into "src/main/webapp/webjars/${artifact.name}"
            }
        }
    }
}

tasks.getByName('jpi') {
    dependsOn unpackWebJars
}

clean {
    delete 'src/main/webapp/webjars'
}

jar {
    // See https://www.jenkins.io/blog/2018/01/13/jep-200/
    manifest {
        attributes 'Jenkins-ClassFilter-Whitelisted': 'true'
    }
}
